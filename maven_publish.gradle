apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            artifactId libraryArtifactId
            version libraryVersion
            artifact("$buildDir/outputs/aar/android-app-info-logger-${libraryVersion}-release.aar")

            // generate pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null) {
                        // if(!configurations.embedded.allDependencies.contains(it)) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        // }
                    }
                }
            }
        }
    }
}

artifactory {
    def localPropertiesFile = rootProject.file("local.properties")
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(localPropertiesFile))

    def appInfoLoggerSdkUrl = localProperties['appInfoLoggerSdkUrl']
    def appInfoLoggerSdkRepoKey = localProperties['appInfoLoggerSdkRepoKey']
    def appInfoLoggerSdkUser = localProperties['appInfoLoggerSdkUser']
    def appInfoLoggerSdkPassword = localProperties['appInfoLoggerSdkPassword']

    contextUrl = appInfoLoggerSdkUrl
    publish {
        repository {
            repoKey = appInfoLoggerSdkRepoKey
            username = appInfoLoggerSdkUser
            password = appInfoLoggerSdkPassword
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}