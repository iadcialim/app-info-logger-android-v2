apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            artifactId libraryArtifactId
            version libraryVersion
            artifact("$buildDir/outputs/aar/android-app-info-logger-${libraryVersion}-release.aar")

            // generate pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null) {
                        // if(!configurations.embedded.allDependencies.contains(it)) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        // }
                    }
                }
            }
        }
    }
}

artifactory {
    def localPropertiesFile = rootProject.file("local.properties")
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(localPropertiesFile))

    def jFrogArtifactoryUser = localProperties['jFrogArtifactoryUser']
    def jFrogArtifactoryPassword = localProperties['jFrogArtifactoryPassword']

    contextUrl = 'https://quantuminventions.jfrog.io/artifactory/'
    publish {
        repository {
            repoKey = 'android-libs-release-local'
            username = jFrogArtifactoryUser
            password = jFrogArtifactoryPassword
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}